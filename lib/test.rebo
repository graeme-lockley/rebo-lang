let Std = import("std")

let harness(title) = {
  let tests = []

  let self = {
    start: milliTimestamp(),
    eq: fn(name, result, expected, context) {
      tests := [...tests, {name: name, passed?: result == expected, result: result, expected: expected, context: context, duration: _resetTimestamp!()}]
    },
    eqCatch: fn(f, name, expected, context) {
      let error = false
      let result = f() catch e -> { e.stack := () ; self.eq(name, e, expected, context) ; error := true }

      if !error ->
        tests := [...tests, {name: name, passed?: false, result: result, expected: expected, context: context, duration: _resetTimestamp!()}]
    },
    assert: fn(name, result) = self.eq(name, result, true),
    suite: fn(name, runner) {
        let r = harness(title + ": " + name)
        tests := [...tests, r]

        _resetTimestamp!()

        r
    },
    report: fn(totals = false, silent = false) = _reportSuite(title, silent, tests, totals, self.start)
  }

  _resetTimestamp!()

  self
}

let _suite?(t) = t.passed? == ()
let _test?(t) = t.passed? != ()

let _reportSuite(title, silent, tests, totals, startTime) {
  let endTime = milliTimestamp() - startTime

  let ansi = Std.ansi

  let actuals = tests |> Std.filter(_test?)

  if !silent && len(actuals) > 0 -> {
    println(ansi.foreground.lightGray, "run ", len(actuals), " tests from ", title, " (", endTime, "ms)", ansi.control.reset);

    actuals |> Std.each(fn(t) = {
      let name = t.name
      let passed = t.passed?
      let result = t.result
      let expected = t.expected
      let duration = t.duration

      if passed ->
        println(ansi.foreground.green, "✓ ", ansi.control.reset, name, ansi.foreground.lightGray, " (", duration, "ms)", ansi.control.reset)
      | {
          println(ansi.foreground.red, "✗ ", ansi.control.reset, name, ansi.foreground.lightGray, " (", duration, "ms)", ansi.control.reset)
          println("  expected: ", expected)
          println("  actual: ", result)
          if t.context != () -> println("  context: ", t.context)
        }
    })
  }

  let suiteTotals = {
    total: len(actuals),
    passed: len(Std.filter(actuals, fn(test) test.passed?)),
    failed: len(Std.filter(actuals, fn(test) !test.passed?))
  }

  tests 
    |> Std.filter(_suite?) 
    |> Std.each(fn(suite) _addTotal!(suiteTotals, suite.report(title, silent)))

  if totals -> {
    println()

    if suiteTotals.failed == 0 ->
      print(ansi.foreground.green, "✓ ", ansi.control.reset)
    | print(ansi.foreground.red, "✗ ", ansi.control.reset)

    println(suiteTotals.total, " total | ", suiteTotals.passed, " passed | ", suiteTotals.failed, " failed ", ansi.foreground.lightGray, "(", endTime, "ms)", ansi.control.reset)
  }

  suiteTotals
}

let _timestamp = 0

let _resetTimestamp!() {
  let current = _timestamp
  _timestamp := milliTimestamp()
  _timestamp - current
}

let _addTotal!(totals, t) {
  totals.total := totals.total + t.total;
  totals.passed := totals.passed + t.passed;
  totals.failed := totals.failed + t.failed;
}