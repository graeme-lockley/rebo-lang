let FS = import("fs")
let Path = import("path")
let Str = import("str")

# The directory where the rebo binary is located.  This function is used particularly to locate the rebo
# utilities that form part of the rebo distribution.
let binHome() {    
   let exe = rebo.exe

   if exe |> Str.endsWith?("/zig-out/bin/rebo") -> Str.dropEnd(exe, 17) + "/bin"
    | Path.dir(exe)
}

let compile = rebo.os["bc.compile"]

let eval(bc, s) = rebo.os["bc.eval"](bc, s ? scope())

let padLeft(s, n) = " " * (n - len(s)) + s

let dis(bc, indent = "") {
   let ip = 0

   while ip < len(bc) -> {
      print(indent, ansi.foreground.lightGray, padLeft(str(ip), 6), ": ", ansi.control.reset)

      let op = int(bc[ip])

      if op > len(ops()) -> { println("unknown opcode: ", op) ; ip := ip + 1 }
       | {
         print(ops()[op][0])

         ip := ip + 1

         each(ops()[op][1], fn(i) {
            match i
            | 'I' -> { print(" ", ansi.foreground.green, _readInt(bc, ip), ansi.control.reset) ; ip := ip + 8 }
            | 'C' -> { print(" ", ansi.foreground.green, bc[ip], ansi.control.reset) ; ip := ip + 1 }
            | 'F' -> { print(" ", ansi.foreground.green, _readFloat(bc, ip), ansi.control.reset) ; ip := ip + 8 }
            | 'P' -> { print(" ", ansi.foreground.lightGray, _position(bc, ip), ansi.control.reset) ; ip := ip + 16 }
            | 'S' -> {
               let s = _readString(bc, ip)
               print(" ", ansi.foreground.green, str(s, true), ansi.control.reset)
               ip := _advanceOverString(ip, s)
            }
            | 'B' -> {
               let args = _readInt(bc, ip)
               ip := ip + 8
               while args > 0 -> {
                  let name = _readString(bc, ip)
                  ip := _advanceOverString(ip, name)

                  println()
                  print(indent, "          .", name)

                  let default = _readString(bc, ip)
                  ip := _advanceOverString(ip, default)

                  if len(default) > 0 -> {
                     println()
                     dis(default, indent + "        ")
                  }

                  args := args - 1
               }

               let restName = _readString(bc, ip)
               ip := _advanceOverString(ip, restName)
               if len(restName) > 0 -> {
                  println()
                  print(indent, "          ...", restName)
               }

               let body = _readString(bc, ip)
               ip := _advanceOverString(ip, body)

               println()
               dis(body, indent + "    ")
            }
         })

         println()
      }
   }
}

let _readInt = rebo.os["bc.readInt"]

let _readFloat = rebo.os["bc.readFloat"]

let _readString(bc, ip) {
   let len = _readInt(bc, ip)
   bc[ip + 8:ip + 8 + len]
}

let _advanceOverString(ip, str) = ip + 8 + len(str)

let _position(bc, ip) {
   let start = _readInt(bc, ip)
   let end = _readInt(bc, ip + 8)

   if start == end -> str(start) 
    | (str(start) + "-" + str(end))
}

let _ops = ()

let ops() {
   if _ops == () -> _ops := _loadOpsFile(binHome() + "/../src/bc-interpreter/ops.zig")

   _ops
}

let _loadOpsFile(path) =
   FS.lines(path)
      |> map(fn(l) Str.trim(l)) 
      |> filter(fn(line) len(line) > 0 && !Str.startsWith?(line, "pub const") && !Str.startsWith?(line, "}")) 
      |> map(fn(l) split(l, "//")) 
      |> map(fn(l) [Str.dropEnd(Str.trim(l[0]), 1), Str.trim(l[1] ? "")])
