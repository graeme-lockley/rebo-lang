uses "./Scanner.llld";

Module
    : {Expression {";"}}
    ;

Expression
    : "let" Pattern "=" Expression
    | "let" Identifier FunctionTail
    | "if" ["|"] Expression "->" Expression {"|" Expression "->" Expression}
    | "match" Expression MatchCase {MatchCase}
    | Or [":=" Expression]
    ;

MatchCase
    : "|" Pattern "->" Expression
    ;

Pattern
    : "(" [Pattern] ")"
    | Identifier
    | LiteralChar
    | LiteralFloat
    | LiteralInt
    | LiteralString
    | "[" [Pattern {"," Pattern} ["," "..." Identifier]] "]" ["@" Identifier]
    | "{" [MapPattern {"," MapPattern}] "}" ["@" Identifier]
    | "true" 
    | "false"
    ;

MapPattern
    : Identifier [":" Pattern] ["@" Identifier]
    ;

Or
    : And {"||" And}
    ;

And
    : Equality {"&&" Equality}
    ;

Equality
    : Relational [("==" | "!=" | "<" | "<=" | ">" | ">=") Relational]
    ;

Additive
    : Multiplicative {["+" | "-"] Multiplicative}
    ;

Multiplicative
    : Qualifier {["*" | "/" | "%"] Qualifier}
    ;

Qualifier
    : Factor { FactorSuffix }
    ;

FactorSuffix
    : "(" [Expression {"," Expression}] ")"
    | "." Identifier
    ;

Factor
    : "(" [Expression] ")"
    | "{" [RecordItem {"," RecordItem}]"}"
    | "{" Expression {";" Expression} "}"
    | Identifier
    | "false"
    | "true"
    | LiteralChar
    | LiteralFloat
    | LiteralInt
    | LiteralString
    | "[" [Expression {"," ["..."] Expression}] "]"
    | "fn" FunctionTail
    ;

FunctionTail
    : Parameters "=" Expression
    ;

RecordItem
    : [LiteralString | Identifier] ":" Expression
    | "..." Expression
    ;

Parameters
    : "(" [Parameter {"," Parameter}] ")"
    ;

Parameter
    : Identifier ["=" Expression]
    ;
