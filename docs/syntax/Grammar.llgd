uses "./Scanner.llld";

Module
    : {Expression {";"}}
    ;

Expression
    : "let" Identifier "=" Expression
    | "let" Identifier FunctionTail
    | "if" ["|"] Expression "->" Expression {"|" Expression "->" Expression}
    | Additive
    ;

Additive
    : Multiplicative {["+" | "-"] Multiplicative}
    ;

Multiplicative
    : Qualifier {["*" | "/"] Qualifier}
    ;

Qualifier
    : Factor { FactorSuffix }
    ;

FactorSuffix
    : "(" [Expression {"," Expression}] ")"
    | "." Identifier
    ;

Factor
    : "(" [Expression] ")"
    | "{" [RecordItem {"," RecordItem}]"}"
    | "{" Expression {";" Expression} "}"
    | Identifier
    | "false"
    | "true"
    | LiteralChar
    | LiteralInt
    | LiteralSymbol
    | "[" [Expression {"," Expression}] "]"
    | "fn" FunctionTail
    ;

FunctionTail
    : Parameters "=" Expression
    ;

RecordItem
    : [LiteralString | Identifier] ":" Expression
    ;

Parameters
    : "(" [Parameter {"," Parameter}] ")"
    ;

Parameter
    : Identifier ["=" Expression]
    ;
