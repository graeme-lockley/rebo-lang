#!env rebo

let CLI = import("../lib/cli.rebo")
let Std = import("../lib/std.rebo")
let Str = import("../lib/str.rebo")
let Test = import("../lib/test.rebo")

let cli = CLI.parse({
  cmd: false,
  enforcing: true,
  opts: {
    silent: false,
    totals: true
  }    
})

let test = Test.harness("suite")

let normaliseName(name) {
  if name[0] == '/' -> name
  | {
      let dir = cwd()

      if Str.endsWith?(dir, "/") -> dir + name
      | dir + "/" + name
    }
}

if len(cli.args) == 0 -> 
  cli.args := import("../lib/fs.rebo").files(".", { predicate: fn(n) Str.endsWith?(n, ".test.rebo") })

cli.args
  |> Std.each(fn(name) {
      let module = normaliseName(name) |> import()

      if module.run == () -> {
        println("Error: File ", name, " did not load correctly or does not have a run function: ", module)
        exit(1)
      } | {
        let newSuite = test.suite(name)
          
        newSuite |> module.run()
        newSuite.report(false, cli.opts.silent)
      }
    })

let stats = test.report(cli.opts.totals, true)

exit(stats.failed)
